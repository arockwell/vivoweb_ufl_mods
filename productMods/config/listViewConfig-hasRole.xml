<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- $This file is distributed under the terms of the license in /doc/license.txt$ -->

<!-- See guidelines in vitro/doc/list_view_configuration_guidelines.txt -->

<list-view-config>
    <query-base>    
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX afn:  &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        
        SELECT DISTINCT ?role
                        ?roleLabel 
                        ?activity ?activityName
                        ?activityLabel                        
                        ?dateTimeStart ?dateTimeEnd WHERE {
            ?subject ?property ?role 
            OPTIONAL {  ?role core:roleIn ?activity 
                        LET (?activityName := afn:localname(?activity)) 
                        OPTIONAL { ?activity rdfs:label ?activityLabel }
            }
            OPTIONAL { ?role rdfs:label ?roleLabel }
            OPTIONAL { ?role core:dateTimeInterval ?dateTimeInterval 
                       OPTIONAL {  ?dateTimeInterval core:start ?dateTimeStartValue . 
                                   ?dateTimeStartValue core:dateTime ?dateTimeStart 
                       }
                       OPTIONAL { ?dateTimeInterval core:end ?dateTimeEndValue .
                                  ?dateTimeEndValue core:dateTime ?dateTimeEnd 
                       }
            }
        } ORDER BY DESC(?dateTimeEnd) DESC(?dateTimeStart) ?personName
    </query-base>
    
    <query-collated>    
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX afn:  &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        
        SELECT DISTINCT ?subclass
                        ?role 
                        ?roleLabel 
                        ?activity ?activityName 
                        ?activityLabel
                        ?dateTimeStart ?dateTimeEnd WHERE {
            ?subject ?property ?role  
            OPTIONAL {  ?role core:roleIn ?activity 
                        LET (?activityName := afn:localname(?activity)) 
                        OPTIONAL { ?activity rdfs:label ?activityLabel }
            }
            OPTIONAL { ?role a ?subclass .
                       ?subclass rdfs:subClassOf core:Role 
            }   
            OPTIONAL { ?role rdfs:label ?roleLabel }
            OPTIONAL { ?role core:dateTimeInterval ?dateTimeInterval 
                       OPTIONAL { ?dateTimeInterval core:start ?dateTimeStartValue .
                                  ?dateTimeStartValue core:dateTime ?dateTimeStart 
                       }
                       OPTIONAL { ?dateTimeInterval core:end ?dateTimeEndValue .
                                  ?dateTimeEndValue core:dateTime ?dateTimeEnd 
                       }
            }
        } ORDER BY ?subclass DESC(?dateTimeEnd) DESC(?dateTimeStart) ?personName
    </query-collated>
    
    <query-construct>
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;  
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;  
        CONSTRUCT {
            ?subclass rdfs:subClassOf core:Role 
        } WHERE {
            ?subclass rdfs:subClassOf core:Role 
        }
    </query-construct>
    
    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;    
        CONSTRUCT { 
            ?subject ?property ?role .
            ?role ?roleProperty ?roleValue . 
            ?activity rdfs:label ?activityName 
        } WHERE {
            {
               ?subject ?property ?role 
            } UNION {
               ?subject ?property ?role .
               ?role ?roleProperty ?roleValue 
            } UNION {
               ?subject ?property ?role .
               ?role core:roleIn ?activity . 
               ?activity rdfs:label ?activityName 
            }
        } 
    </query-construct>
    
    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;   
        CONSTRUCT { 
            ?subject ?property ?role .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:start ?dateTimeStartValue .
            ?dateTimeStartValue core:dateTime ?dateTimeStart 
        } WHERE {
            ?subject ?property ?role .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:start ?dateTimeStartValue .
            ?dateTimeStartValue core:dateTime ?dateTimeStart 
        } 
    </query-construct>

    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;   
        CONSTRUCT { 
            ?subject ?property ?role .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:end ?dateTimeEndValue .
            ?dateTimeEndValue core:dateTime ?dateTimeEnd 
        } WHERE {
            ?subject ?property ?role .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:end ?dateTimeEndValue .
            ?dateTimeEndValue core:dateTime ?dateTimeEnd 
        } 
    </query-construct>

    <template>propStatement-hasRole.ftl</template>
</list-view-config>